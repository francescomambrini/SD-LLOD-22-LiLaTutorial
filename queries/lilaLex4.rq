PREFIX marl: <http://www.gsi.dit.upm.es/ontologies/marl/ns#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX lime: <http://www.w3.org/ns/lemon/lime#>
PREFIX lemonEty: <http://lari-datasets.ilc.cnr.it/lemonEty#>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>

# Pretty complex query: synset that have both positive and negative words
#
# - A synset with a definition
# - evoked by a LE with a Lemma_1
# - same lemma used for a LE of LatinAffectus
# - with a Positive polarity
# - in the same Synset there is a Lemma_2
# - linked with a word of LatinAffectus with Negative polarity

SELECT distinct ?syn ?def ?lemma1 ?l1_lab ?lemma2 ?l2_lab  {
  ?syn a ontolex:LexicalConcept ;
     skos:definition ?def ;
     ontolex:isEvokedBy/ontolex:canonicalForm ?lemma1 .
  ?lemma1 ^ontolex:canonicalForm ?le1 .
  ?le1 ^lime:entry <http://lila-erc.eu/data/lexicalResources/LatinAffectus/Lexicon> ;
         ontolex:sense/marl:hasPolarity marl:Positive ;
         rdfs:label ?l1_lab .
  ?syn ontolex:isEvokedBy/ontolex:canonicalForm ?lemma2 .
  ?lemma2 ^ontolex:canonicalForm ?le2 .
  ?le2 ^lime:entry <http://lila-erc.eu/data/lexicalResources/LatinAffectus/Lexicon> ;
         ontolex:sense/marl:hasPolarity marl:Negative ;
         rdfs:label ?l2_lab .
  FILTER(?lemma1 != ?lemma2)
}
order by ?syn