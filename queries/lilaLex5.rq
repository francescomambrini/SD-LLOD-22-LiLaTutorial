PREFIX marl: <http://www.gsi.dit.upm.es/ontologies/marl/ns#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX lime: <http://www.w3.org/ns/lemon/lime#>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>

# Using SentiWordnet: words that have both Positive and Negative Senses

SELECT distinct ?le ?lab ?s1 ?def1 ?def2 {
  ?le a ontolex:LexicalEntry ;
       rdfs:label ?lab ;
      ^lime:entry <http://lila-erc.eu/data/lexicalResources/LatinWordNet/Lexicon> ;
       ontolex:sense ?s1, ?s2 .
  ?s1 ontolex:isLexicalizedSenseOf/skos:definition ?def1 ;
       marl:hasPolarity marl:Positive .
  ?s2 ontolex:isLexicalizedSenseOf/skos:definition ?def2 ;
       marl:hasPolarity marl:Negative .
    FILTER(?s1 != ?s2)
}